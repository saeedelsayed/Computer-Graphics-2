#version 430

// set this to 1 in order to write fragments to the a-buffer
#define USE_A_BUFFER 0

/*
The following interface is implemented in this shader:
//***** begin interface of fragment.glfs ***********************************
// scalar gamma is not used anymore in fragment.glfs but set to average(gamma3[0..2]) for convenience
uniform float gamma = 2.2;
// per channel gamma value
uniform vec3 gamma3 = vec3(2.2);
void finish_fragment(vec4 color);
void finish_fragment(vec4 color, float depth);
//***** end interface of fragment.glfs ***********************************
*/

uniform bool alpha_test = false;
uniform float alpha_cutoff = 0.1;

#if USE_A_BUFFER == 0
uniform vec3 gamma3 = vec3(2.2);
out vec4 frag_color;
#else
//***** begin interface of a_buffer.glsl ***********************************
bool a_buffer_gather_and_blend(in vec2 frag_xy, out vec4 rgba, out float depth);
bool a_buffer_insert_fragment(vec3 frag_xyz, vec4 rgba);
//***** end interface of a_buffer.glsl ***********************************
#endif

void finish_fragment(vec4 color)
{
	// alpha test
	if (alpha_test && color.a <= alpha_cutoff)
		discard;

#if USE_A_BUFFER == 0
	frag_color = color;
	// perform gamma correction by applying the inverse of the set per-channel gamma value
	frag_color.rgb = pow(frag_color.rgb, 1.0/gamma3);
#else
	a_buffer_insert_fragment(gl_FragCoord.xyz, color);
#endif
}

void finish_fragment(vec4 color, float depth)
{
	// alpha test
	if (alpha_test && color.a <= alpha_cutoff)
		discard;

#if USE_A_BUFFER == 0
	frag_color = color;
	// perform gamma correction by applying the inverse of the set per-channel gamma value
	frag_color.rgb = pow(frag_color.rgb, 1.0/gamma3);

	gl_FragDepth = depth;
#else
	a_buffer_insert_fragment(vec3(gl_FragCoord.xy, depth), color);
#endif
}