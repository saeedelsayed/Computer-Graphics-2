#version 430
#extension GL_EXT_geometry_shader4 : enable

#define CAPS 1

// 0 - Flat
// 1 - Rounded
#define CAP_TYPE 0

//***** begin interface of view.glsl ***********************************
mat4 get_modelview_matrix();
mat4 get_projection_matrix();
mat4 get_modelview_projection_matrix();
mat4 get_inverse_modelview_matrix();
mat4 get_inverse_modelview_projection_matrix();
mat3 get_normal_matrix();
mat3 get_inverse_normal_matrix();
//***** end interface of view.glsl ***********************************

layout(lines) in;
layout(triangle_strip, max_vertices = 14) out;

in vec4 color_gs[];

out vec3 position;
out flat vec4 start_fs;
out flat vec4 end_fs;
out flat vec3 normal_fs;
out flat vec3 binormal_fs;
out flat vec4 color_start_fs;
out flat vec4 color_end_fs;

vec3 ortho_vec(vec3 v) {

	return abs(v.x) > abs(v.z) ? vec3(-v.y, v.x, 0.0f) : vec3(0.0f, -v.z, v.y);
}

void main()
{
	vec4 start = gl_in[0].gl_Position;
	vec4 end = gl_in[1].gl_Position;

	start_fs = start;
	end_fs = end;
	color_start_fs = color_gs[0];
	color_end_fs = color_gs[1];

	float l = length(end.xyz - start.xyz);
	vec3 delta = (end.xyz - start.xyz) / l;

	// start and end radius
	float rs = start.w;
	float re = end.w;
	// maximal diameter
	float max_d = 2 * max(rs, re);

	vec3 center = start.xyz + end.xyz;

#if CAP_TYPE == 1
	// offset center depending on cap radii
	center += delta*(re - rs);
#endif

	center *= 0.5;

	// create a matrix that transforms the unit cube into the cone bounding box as T = (translation * rotation * scale)
	// rotation part
	mat4 T = mat4(1.0);
	T[0] = vec4(delta, 0.0);
	T[1] = vec4(normalize(ortho_vec(delta)), 0.0);
	T[2] = vec4(cross(delta, T[1].xyz), 0.0);
	T[3] = vec4(center, 1.0); // translation part

	// get tangent frame from rotation matrix before scaling is applied
	normal_fs = T[1].xyz;
	binormal_fs = T[2].xyz;

	// scale part
#if CAP_TYPE == 1
	T[0] *= l + rs + re;
	T[1] *= max_d;
	T[2] *= max_d;
#else
	// for flat caps the start and end face will be scaled independently using the input point coordinates
	T[0] *= l;
#endif

	// generate transformed base vector corners of the box in object-space
#if CAP_TYPE == 0
	// for flat caps the start and end radius is used here to determine the scale
	vec4 C0 = T * vec4(-0.5, -rs, -rs, 1.0);
	vec4 C1 = T * vec4(-0.5, -rs, +rs, 1.0);
	vec4 C2 = T * vec4(-0.5, +rs, -rs, 1.0);
	
	vec4 C4 = T * vec4(+0.5, -re, -re, 1.0);
	vec4 C5 = T * vec4(+0.5, -re, +re, 1.0);
	vec4 C6 = T * vec4(+0.5, +re, -re, 1.0);
	
	// calculate remaining corners
	vec4 C3 = C1 + C2 - C0;
	vec4 C7 = C5 + C6 - C4;
#else
	// for rounded caps we use unit coordinates as the full scaling is given in T
	vec4 C0 = T * vec4(-0.5, -0.5, -0.5, 1.0);
	vec4 C1 = T * vec4(0.5, -0.5, -0.5, 1.0);
	vec4 C2 = T * vec4(-0.5, 0.5, -0.5, 1.0);
	vec4 C4 = T * vec4(-0.5, -0.5, 0.5, 1.0);

	// calculate remaining corners
	vec4 D20 = C2 - C0;

	vec4 C3 = C1 + D20;
	vec4 C5 = C1 + C4-C0;
	vec4 C6 = C4 + D20;
	vec4 C7 = C5 + D20;
#endif
	
	mat4 M = get_modelview_projection_matrix();

	// output position to fragment shader in object-space
	position = C6.xyz;
	gl_Position = M * C6;
	EmitVertex();

	position = C7.xyz;
	gl_Position = M * C7;
	EmitVertex();

	position = C4.xyz;
	gl_Position = M * C4;
	EmitVertex();

	position = C5.xyz;
	gl_Position = M * C5;
	EmitVertex();

	position = C1.xyz;
	gl_Position = M * C1;
	EmitVertex();

	position = C7.xyz;
	gl_Position = M * C7;
	EmitVertex();
	
	position = C3.xyz;
	gl_Position = M * C3;
	EmitVertex();
	
	position = C6.xyz;
	gl_Position = M * C6;
	EmitVertex();
	
	position = C2.xyz;
	gl_Position = M * C2;
	EmitVertex();
	
	position = C4.xyz;
	gl_Position = M * C4;
	EmitVertex();
	
	position = C0.xyz;
	gl_Position = M * C0;
	EmitVertex();
	
	position = C1.xyz;
	gl_Position = M * C1;
	EmitVertex();
	
	position = C2.xyz;
	gl_Position = M * C2;
	EmitVertex();
	
	position = C3.xyz;
	gl_Position = M * C3;
	EmitVertex();

	EndPrimitive();
}
