#version 430

//***** begin interface of shape2d.glsl ***********************************
uniform vec4 border_color = vec4(1.0);
uniform float border_width = 0.0;
uniform float border_radius = 0.0;
uniform float feather_origin = 0.5;
//uniform vec2 tex_scaling = vec2(1.0);

uniform bool use_smooth_feather = false;

float get_feather_width();
vec2 get_shape_size();
vec4 get_active_color(vec4 color);
vec4 transform_world_to_window_space(vec2 p);
//***** end interface of shape2d.glsl ***********************************

//***** begin interface of fragment2d.glfs ***********************************
void finish_sdf_fragment2d(in float signed_distance, in vec4 color);
void finish_sdf_fragment2d(in float signed_distance, in vec4 color, in vec2 texcoords);
void finish_fragment2d(in vec4 color);
void finish_fragment2d(in vec4 color, in vec2 texcoords);
//***** end interface of fragment2d.glfs ***********************************

uniform float stem_width;
uniform float head_width;

in vec2 pix_coord;
in vec2 tex_coord;
in flat vec2 pos0;
in flat vec2 pos1;
in vec4 col;
in flat float hl;

// https://www.shadertoy.com/view/slj3Dd
// The arrow goes from a to b. It's thickness is w.x. The arrow
// head's thickness is w.y.
float sd_arrow(in vec2 p, vec2 a, vec2 b, vec2 w, float fw)
{
	float cc = 0.5*head_width;
	float dd = border_radius / cc;

	float head_length = hl + dd*hl;
	float head_ratio = (2.0*head_length - feather_origin*fw) / (head_width + feather_origin*fw);

    // constant setup
    const float k = head_ratio;
	vec2  ba = b - a;
    float l2 = dot(ba,ba);
    float l = sqrt(l2);

    // pixel setup
    p = p-a;
    p = mat2(ba.x,-ba.y,ba.y,ba.x)*p/l;
    p.y = abs(p.y);
    vec2 pz = p-vec2(l-w.y*k,w.y);

    // === distance (four segments) === 

    vec2 q = p;
    q.x -= clamp( q.x, 0.0, l-w.y*k );
    q.y -= w.x;
    float di = dot(q,q);
    //----
    q = pz;
    q.y -= clamp( q.y, w.x-w.y, 0.0 );
    di = min( di, dot(q,q) );
    //----
    if( p.x<w.x ) // conditional is optional
    {
    q = p;
    q.y -= clamp( q.y, 0.0, w.x );
    di = min( di, dot(q,q) );
    }
    //----
    if( pz.x>0.0 ) // conditional is optional
    {
    q = pz;
    q -= vec2(k,-1.0)*clamp( (q.x*k-q.y)/(k*k+1.0), 0.0, w.y );
    di = min( di, dot(q,q) );
    }
    
    // === sign === 
    
    float si = 1.0;
    float z = l - p.x;
    if( min(p.x,z)>0.0 ) //if( p.x>0.0 && z>0.0 )
    {
      float h = (pz.x<0.0) ? w.x : z/k;
      if( p.y<h ) si = -1.0;
    }
    return si*sqrt(di);
}

void main() {
	
	float fw = get_feather_width();
	vec2 thickness = 0.5*vec2(stem_width, head_width);
	float dist = sd_arrow(pix_coord, pos0, pos1, thickness - border_radius + feather_origin*fw, fw);

	vec4 color = get_active_color(col);
	finish_sdf_fragment2d(dist, color, tex_coord);
}
