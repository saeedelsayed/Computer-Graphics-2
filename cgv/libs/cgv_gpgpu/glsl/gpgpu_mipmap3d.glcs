#version 430

layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout(binding = 0) uniform sampler3D input_tex;
layout(binding = 1) uniform writeonly image3D output_tex;

uniform uint level;
uniform uvec3 output_size;

vec4 sample_texture(in vec3 coords) {
	
	return textureLod(input_tex, coords, level);
}

void main() {
	
	// the texel coordinates of this thread
    ivec3 idx = ivec3(gl_GlobalInvocationID);
    
	// this produces correct results even for odd sized input textures
	if(idx.x < output_size.x && idx.y < output_size.y && idx.z < output_size.z) {
		vec3 output_size_float = vec3(output_size);
		vec3 double_size = 2.0 * output_size_float;

		vec3 fidx = idx / output_size_float;
		vec3 fstep = 1.0 / double_size;
		fidx += 0.5 * fstep;

		vec4 result = vec4(0.0);
		// use texture sampler filtering to average samples
		result += sample_texture(fidx + vec3(0.0, 0.0, 0.0));
		result += sample_texture(fidx + vec3(fstep.x, 0.0, 0.0));
		result += sample_texture(fidx + vec3(0.0, fstep.y, 0.0));
		result += sample_texture(fidx + vec3(fstep.x, fstep.y, 0.0));
		result += sample_texture(fidx + vec3(0.0, 0.0, fstep.z));
		result += sample_texture(fidx + vec3(fstep.x, 0.0, fstep.z));
		result += sample_texture(fidx + vec3(0.0, fstep.y, fstep.z));
		result += sample_texture(fidx + vec3(fstep.x, fstep.y, fstep.z));

		result /= 8.0;

		imageStore(output_tex, idx, result);
	}
}
